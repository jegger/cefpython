; Parts of this code was taken from wxPython/distrib/make_installer.py

[Setup]

AppName = CEF Python 3 for Python %(PYTHON_VERSION)s
AppVersion = %(APP_VERSION)s
AppVerName = CEF Python 3 version %(APP_VERSION)s for Python %(PYTHON_VERSION)s %(PYTHON_ARCHITECTURE)s

AppPublisher = Czarek Tomczak
AppPublisherURL = http://code.google.com/cefpython/
AppSupportURL = https://groups.google.com/group/cefpython?hl=en
AppUpdatesURL = http://code.google.com/cefpython/
AppCopyright = Copyright 2012-2013 Czarek Tomczak

DefaultDirName = {code:GetInstallDir|c:\Python}

DefaultGroupName = CEF Python 3 for Python %(PYTHON_VERSION)s
PrivilegesRequired = none
DisableStartupPrompt = yes
Compression = zip
DirExistsWarning = no
DisableReadyMemo = yes
DisableReadyPage = yes
DisableDirPage = no
DisableProgramGroupPage = no
UsePreviousAppDir = yes
UsePreviousGroup = yes

SourceDir = %(BINARIES_DIR)s
OutputDir = %(INSTALLER_DIR)s\Output
OutputBaseFilename = %(PACKAGE_NAME)s-%(APP_VERSION)s.win32-py%(PYTHON_VERSION)s-innosetup

UninstallFilesDir = {app}\%(PACKAGE_NAME)s
LicenseFile = %(BINARIES_DIR)s\LICENSE.txt

[Icons]

Name: "{group}\Examples"; Filename: "{app}\%(PACKAGE_NAME)s\examples";
Name: "{group}\Uninstall Package"; Filename: "{uninstallexe}";

[Run]

Filename: "{app}\%(PACKAGE_NAME)s\examples"; Flags: postinstall skipifsilent shellexec;

[Files]

Source: "*.dll"; DestDir: "{app}\%(PACKAGE_NAME)s"; Flags: ignoreversion;
Source: "*.pak"; DestDir: "{app}\%(PACKAGE_NAME)s"; Flags: ignoreversion;
Source: "locales\*.pak"; DestDir: "{app}\%(PACKAGE_NAME)s\locales"; Flags: ignoreversion;
Source: "%(INSTALLER_DIR)s\__init__.py.generated"; DestDir: "{app}\%(PACKAGE_NAME)s"; DestName: "__init__.py"; Flags: ignoreversion;
Source: "cefclient.exe"; DestDir: "{app}\%(PACKAGE_NAME)s"; Flags: ignoreversion;
Source: "cefpython_py%(PYTHON_VERSION_NODOT)s.pyd"; DestDir: "{app}\%(PACKAGE_NAME)s"; Flags: ignoreversion;
Source: "LICENSE.txt"; DestDir: "{app}\%(PACKAGE_NAME)s"; Flags: ignoreversion;
Source: "Microsoft.VC90.CRT.manifest"; DestDir: "{app}\%(PACKAGE_NAME)s"; Flags: ignoreversion;
Source: "README.txt"; DestDir: "{app}\%(PACKAGE_NAME)s"; Flags: ignoreversion;
Source: "subprocess.exe"; DestDir: "{app}\%(PACKAGE_NAME)s"; Flags: ignoreversion;

; ------------------------------------------------------------------------------
; wx subpackage
; ------------------------------------------------------------------------------

Source: "%(WX_SUBPACKAGE_DIR)s\*.py"; DestDir: "{app}\%(PACKAGE_NAME)s\wx"; Flags: ignoreversion;
Source: "%(WX_SUBPACKAGE_DIR)s\*.txt"; DestDir: "{app}\%(PACKAGE_NAME)s\wx"; Flags: ignoreversion;
Source: "%(WX_SUBPACKAGE_DIR)s\images\*.png"; DestDir: "{app}\%(PACKAGE_NAME)s\wx\images"; Flags: ignoreversion;

; ------------------------------------------------------------------------------
; wx examples
; ------------------------------------------------------------------------------

Source: "%(WX_SUBPACKAGE_DIR)s\examples\*.py"; DestDir: "{app}\%(PACKAGE_NAME)s\examples\wx"; Flags: ignoreversion;
Source: "%(WX_SUBPACKAGE_DIR)s\examples\*.html"; DestDir: "{app}\%(PACKAGE_NAME)s\examples\wx"; Flags: ignoreversion;
Source: "%(WX_SUBPACKAGE_DIR)s\examples\*.js"; DestDir: "{app}\%(PACKAGE_NAME)s\examples\wx"; Flags: ignoreversion;
Source: "%(WX_SUBPACKAGE_DIR)s\examples\*.css"; DestDir: "{app}\%(PACKAGE_NAME)s\examples\wx"; Flags: ignoreversion;
Source: "%(WX_SUBPACKAGE_DIR)s\examples\*.png"; DestDir: "{app}\%(PACKAGE_NAME)s\examples\wx"; Flags: ignoreversion;

; ------------------------------------------------------------------------------
; examples
; ------------------------------------------------------------------------------

Source: "cefwindow.py"; DestDir: "{app}\%(PACKAGE_NAME)s\examples"; Flags: ignoreversion;
Source: "example.py"; DestDir: "{app}\%(PACKAGE_NAME)s\examples"; Flags: ignoreversion;
Source: "*.html"; DestDir: "{app}\%(PACKAGE_NAME)s\examples"; Flags: ignoreversion;
Source: "*.css"; DestDir: "{app}\%(PACKAGE_NAME)s\examples"; Flags: ignoreversion;
Source: "*.js"; DestDir: "{app}\%(PACKAGE_NAME)s\examples"; Flags: ignoreversion;
Source: "icon.ico"; DestDir: "{app}\%(PACKAGE_NAME)s\examples"; Flags: ignoreversion;
Source: "pygtk_.py"; DestDir: "{app}\%(PACKAGE_NAME)s\examples"; Flags: ignoreversion;
Source: "pyqt.py"; DestDir: "{app}\%(PACKAGE_NAME)s\examples"; Flags: ignoreversion;
Source: "pyside.py"; DestDir: "{app}\%(PACKAGE_NAME)s\examples"; Flags: ignoreversion;
Source: "wxpython.py"; DestDir: "{app}\%(PACKAGE_NAME)s\examples"; Flags: ignoreversion;

[UninstallDelete]

Type: files; Name: "{app}\%(PACKAGE_NAME)s\*.pyc";
Type: files; Name: "{app}\%(PACKAGE_NAME)s\examples\*.pyc";
Type: files; Name: "{app}\%(PACKAGE_NAME)s\examples\*.log";
Type: filesandordirs; Name: "{app}\%(PACKAGE_NAME)s\__pycache__"
Type: files; Name: "{app}\%(PACKAGE_NAME)s\examples\wx\*.pyc";
Type: files; Name: "{app}\%(PACKAGE_NAME)s\examples\wx\*.log";
Type: files; Name: "{app}\%(PACKAGE_NAME)s\wx\*.pyc";
Type: files; Name: "{app}\%(PACKAGE_NAME)s\wx\*.log";

; Issue 67 - remaining files in the wx/ and examples/wx/ directories:
; https://code.google.com/p/cefpython/issues/detail?id=67
; Just delete them all:
Type: filesandordirs; Name: "{app}\%(PACKAGE_NAME)s\examples\wx"
Type: filesandordirs; Name: "{app}\%(PACKAGE_NAME)s\wx"

[Code]

program Setup;
var
    PythonDir  : String;
    InstallDir : String;

function InitializeSetup(): Boolean;
begin

    if not RegQueryStringValue(HKEY_CURRENT_USER,
            'Software\Python\PythonCore\%(PYTHON_VERSION)s\InstallPath',
            '', PythonDir) then begin

        if not RegQueryStringValue(HKEY_LOCAL_MACHINE,
                'Software\Python\PythonCore\%(PYTHON_VERSION)s\InstallPath',
                '', PythonDir) then begin

            if not RegQueryStringValue(HKEY_CURRENT_USER,
                    'Software\Wow6432Node\Python\PythonCore\%(PYTHON_VERSION)s\InstallPath',
                    '', PythonDir) then begin

                if not RegQueryStringValue(HKEY_LOCAL_MACHINE,
                        'Software\Wow6432Node\Python\PythonCore\%(PYTHON_VERSION)s\InstallPath',
                        '', PythonDir) then begin

                    MsgBox('No installation of Python %(PYTHON_VERSION)s '
                           + 'found in registry.' + #13 + 'Be sure to enter '
                           + 'a pathname that places Python on the '
                           + 'PYTHONPATH',
                           mbConfirmation, MB_OK);
                    PythonDir := 'C:\Python';
                end;
            end;
        end;
    end;

    InstallDir := PythonDir + '\Lib\site-packages';
    Result := True;
end;

function GetInstallDir(Default: String): String;
begin
    Result := InstallDir;
end;

function UninstallOld(FileName: String): Boolean;
var
    ResultCode: Integer;
begin
    Result := False;
    if FileExists(FileName) then begin
        Result := True;
        Exec(FileName, '/SILENT', WizardDirValue(), SW_SHOWNORMAL,
             ewWaitUntilTerminated, ResultCode);
    end;
end;

function NextButtonClick(CurPage: Integer): Boolean;
begin
    Result := True;
    if CurPage <> wpSelectDir then Exit;
    UninstallOld(WizardDirValue() + '\%(PACKAGE_NAME)s\unins000.exe')
end;
